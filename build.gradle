plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'io.openapiprocessor.openapi-processor'  version '2022.1'
    id "io.freefair.lombok" version "6.5.0.2"
    id 'jacoco'
}

group = 'com.verint'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    integrationTestImplementation.extendsFrom testImplementation
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate{
        classDirectories.setFrom(files(classDirectories.files.collect{
            fileTree(dir: it, exclude: [
                    'com/verint/**/ToDoMapperImpl.class'
            ])
        }))
    }
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            includes = ['com.verint.todoappapi.*']
            excludes =
                    ['com.verint.todoappapi.ToDoAppApiApplication',
                     'com.verint.todoappapi.ToDoConfig',
                     'com.verint.todoappapi.ToDoMapperImpl']

            limit {
                minimum = 1.0
            }
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.mapstruct:mapstruct:1.5.1.Final'
    implementation 'org.postgresql:postgresql'

    runtimeOnly 'com.h2database:h2'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.1.Final'

    integrationTestImplementation 'io.rest-assured:rest-assured'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'junit:junit'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

openapiProcessor {
    spring {
        processor 'io.openapiprocessor:openapi-processor-spring:2022.3'
        apiPath "$projectDir/src/main/resources/to-do-openapi.yaml"
        targetDir "$projectDir/build/openapi"
        mapping "$projectDir/src/main/resources/mapping.yaml"
        showWarnings true
    }
}

sourceSets {
    main {
        java {
            srcDirs "$projectDir/build/openapi"
        }
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

compileJava.dependsOn('processSpring')
build.dependsOn jacocoTestCoverageVerification
