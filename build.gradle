plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'io.openapiprocessor.openapi-processor'  version '2022.1'
    id "io.freefair.lombok" version "6.5.0.2"
    id 'jacoco'
}

group = 'com.verint'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.0
            }
            excludes = [
                    'com.verint.todoappapi.ToDoAppApiApplication'
            ]
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

openapiProcessor {
    spring {
        processor 'io.openapiprocessor:openapi-processor-spring:2022.3'
        apiPath "$projectDir/src/main/resources/to-do-openapi.yaml"
        targetDir "$projectDir/build/openapi"
        mapping "$projectDir/src/main/resources/mapping.yaml"
        showWarnings true
    }
}

sourceSets {
    main {
        java {
            srcDirs "$projectDir/build/openapi"
        }
    }
}

compileJava.dependsOn('processSpring')
build.dependsOn jacocoTestCoverageVerification
